// Programa...: FrtSaf24
// Autor......: Robert Koch
// Data.......: 07/12/2022
// Descricao..: Calcula valor de frete de entrega de safra, a ser pago aos associados, para a safra 2024.
//              Criado com base no FrtSaf22.prw

// Tags para automatizar catalogo de customizacoes:
// #TipoDePrograma    #Processamento
// #Descricao         #Calcula valor de frete de entrega de safra 2024
// #PalavasChave      #safra #frete_safra
// #TabelasPrincipais #SZI #SZE #SZF
// #Modulos           #Coop

// Historico de alteracoes:
// 05/01/2024 - Robert - Programa criado com base no FrtSaf23.prw
//

// ----------------------------------------------------------------
User Function FrtSaf24 (_sNucleo, _sCadVit, _sFilDest, _nPesoFrt, _sCor, _sGrpFam)
	local _nDist    := 0
	local _nValFre  := 0
	local _oAviso   := NIL
	local _sLinkSrv := U_LkServer ('NAWEB')
	local _aDistKM  := {}
	local _sMetodo  := ''
	local _sMemCalc := ''
	local _nFator   := 0

	_sMemCalc += 'Nucleo.................: ' + _sNucleo + chr (13) + chr (10)
	_sMemCalc += 'Grupo familiar.........: ' + _sGrpFam + chr (13) + chr (10)
	_sMemCalc += 'Propriedade rural......: ' + _sCadVit + chr (13) + chr (10)
	_sMemCalc += 'Filial de entrega......: ' + _sFilDest + chr (13) + chr (10)
	_sMemCalc += 'Peso...................: ' + cvaltochar (_nPesoFrt) + ' Kg' + chr (13) + chr (10)
	_sMemCalc += 'Cor da uva.............: ' + _sCor + chr (13) + chr (10)

	// Busca as distancias da propriedade rural de onde vem a uva
	_oSQL := ClsSQL ():New ()
	_oSQL:_sQuery := ""

	if _sFilDest == '03'
		_oSQL:_sQuery += " SELECT distinct CCPropriedadeKMF03"  // Usa DISTINCT por que pode haver caso de propriedade explorada por mais de 1 grupo familiar
	elseif _sFilDest == '07'
		_oSQL:_sQuery += " SELECT distinct CCPropriedadeKMF07"  // Usa DISTINCT por que pode haver caso de propriedade explorada por mais de 1 grupo familiar
	elseif _sFilDest $ '01/09'
		_oSQL:_sQuery += " SELECT distinct CCPropriedadeKMF01"  // Usa DISTINCT por que pode haver caso de propriedade explorada por mais de 1 grupo familiar
	else
		u_help ("Filial destino '" + _sFilDest + "' sem tratamento no programa " + procname (),, .T.)
		_sMemCalc += "Filial destino '" + _sFilDest + "' sem tratamento no programa " + procname () + chr (13) + chr (10)
		_oAviso := ClsAviso ():New ()
		_oAviso:Tipo       = 'E'
		_oAviso:DestinZZU  = {'143'}  // 143 = grupo da agronomia
		_oAviso:Texto      = "Filial destino '" + _sFilDest + "' sem tratamento no programa de calculo de frete de safra."
		_oAviso:Origem     = procname ()
		_oAviso:Grava ()
	endif
	_oSQL:_sQuery +=   " FROM " + _sLinkSrv + ".CCPropriedade"
	_oSQL:_sQuery +=  " WHERE CCPropriedadeCod = " + cvaltochar (val (_sCadVit))  // NaWeb guarda em formato numerico.
	//_oSQL:Log ()
	_aDistKM = aclone (_oSQL:RetFixo (1, "ao consultar a distancia da propriedade rural '" + _sCadVit + "'. Verifique cadastro no NaWeb.", .T.))
	if len (_aDistKM) == 1
		_nDist = _aDistKM [1, 1] * 2  // ida e volta
	else
		_sMemCalc += "Problemas ao consultar a distancia da propriedade rural '" + _sCadVit + "'. Verifique cadastro no NaWeb." + chr (13) + chr (10)
		_lContinua = .F.
	endif
	_sMemCalc += 'Distancia (ida e volta): ' + cvaltochar (_nDist) + ' Km' + chr (13) + chr (10)

	if _nDist <= 0
		u_help ("Distancia nao informada entre a propriedade " + _sCadVit + " e a filial " + _sFilDest + ". Frete de safra nao pode ser calculado.",, .t.)
		_sMemCalc += "Distancia nao informada entre a propriedade " + _sCadVit + " e a filial " + _sFilDest + ". Frete de safra nao pode ser calculado." + chr (13) + chr (10)
		if IsInCallStack ("U_VA_RUSN")
			_oAviso := ClsAviso ():New ()
			_oAviso:Tipo       = 'E'
			_oAviso:DestinZZU  = {'143'}  // 143 = grupo da agronomia
			_oAviso:Texto      = "Distancia nao informada entre a propriedade " + _sCadVit + " e a filial " + _sFilDest + ". Frete de safra nao pode ser calculado."
			_oAviso:Origem     = procname ()
			_oAviso:Grava ()
		endif
	endif
	
	// Associados que deveriam entregar na F01, mas teimam em levar na F07 - GLPI 13221
	if _sGrpFam $ '000248/001301/002083' .and. _sFilDest $ '07'
		_sMemCalc += "Frete zerado (carga entregue em filial diferente da orientada pela Cooperativa). Permanece o auxilio combustivel." + chr (13) + chr (10)
		_nFator  = 0.3
		_sMetodo = 'D-Caso geral (' + GetMv ("MV_SIMB1") + ' ' + alltrim (transform (_nFator, "@E 999,999.99")) + ' X ton X distancia)'
		_nValFre = _nFator * _nPesoFrt / 1000 * _nDist

	elseif _sCor == 'T' .and. _sNucleo $ 'PB/JC' .and. _sFilDest $ '01/09'
		_nFator  = 0.07
		_sMetodo = 'P-Tintas de Farroupilha entregues na matriz/F09 (' + GetMv ("MV_SIMB1") + ' ' + alltrim (transform (_nFator, "@E 999,999.99")) + ' X peso Kg)'
		_nValFre = _nFator * _nPesoFrt

	elseif _sCor == 'T' .and. _sNucleo $ 'SV/SG/FC/NP' .and. _sFilDest $ '07'
		_nFator  = 0.07
		_sMetodo = 'P-Tintas de Caxias/Flores entregues em Farroupilha (' + GetMv ("MV_SIMB1") + ' ' + alltrim (transform (_nFator, "@E 999,999.99")) + ' X peso Kg)'
		_nValFre = _nFator * _nPesoFrt
	
	else
		_nFator  = 0.3
		_sMetodo = 'D-Caso geral (' + GetMv ("MV_SIMB1") + ' ' + alltrim (transform (_nFator, "@E 999,999.99")) + ' X ton X distancia)'
		_nValFre = _nFator * _nPesoFrt / 1000 * _nDist
	endif

	_nValFre = round (_nValFre, 2)
	_sMemCalc += "Metodo: " + _sMetodo + chr (13) + chr (10)
	_sMemCalc += "Frete calculado: " + GetMv ("MV_SIMB1") + ' ' + alltrim (transform (_nValFre, "@E 999,999,999.99")) + chr (13) + chr (10)

	u_log2 ('info', '[' + procname () + '] Distancia Km..: ' + cvaltochar (_nDist) + ' Metodo: ' + _sMetodo + ' Frt. calculado: ' + cvaltochar (_nValFre))
return {_nValFre, _nDist, _sMetodo, _sMemCalc}
