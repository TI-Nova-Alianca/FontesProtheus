// Programa:  Mta070Mnu
// Autor:     Robert Koch
// Data:      22/08/2022
// Descricao: P.E. para adicionar botoes na tela MATA070 (cadastro de bancos)
//            Criado para informar saldo em banco especifico para fluxo de caixa (GLPI 12486)

// Tags para automatizar catalogo de customizacoes:
// #TipoDePrograma    #ponto_de_entrada
// #Descricao         #P.E. para adicionar botoes na tela MATA070 (cadastro de bancos)
// #PalavasChave      #ponto_de_entrada #bancos #saldo #fluxo_de_caixa #GLPI 12486
// #TabelasPrincipais #SA6
// #Modulos           #FIN

// Historico de alteracoes:
// 31/08/2022 - Robert - Atualiza tambem a tabela SE8 (GLPI 12535)
//

// --------------------------------------------------------------------------
user function Mta070Mnu ()
	local _aRotAdic := {}

	aadd (_aRotAdic, {"Saldo para fluxo", "U_A070Flx ()", 0, 6, 0, NIL})
	
	aadd (aRotina, {"Especificos"           , _aRotAdic, 0, 6, 0, NIL})
return


// --------------------------------------------------------------------------
// Altera saldo em conta criada especificamente para uso no fluxo de caixa.
user function A070Flx ()
	local _lContinua := .T.

	if _lContinua .and. sa6 -> a6_cod != '997'
		u_help ("Esta nao e' a conta bancaria criada para fluxo de caixa.",, .t.)
		_lContinua = .F.
	endif
	if _lContinua
		_lContinua = U_ZZUVL ('142', __cUserId, .t.)
	endif
	if _lContinua
		_nNovoSld = U_Get ('Novo saldo para fluxo de caixa:', 'N', 15, '@E 999,999,999.999.99', '', sa6 -> a6_salatu, .F., '.t.')
		if valtype (_nNovoSld) == 'N'  // Retorna NIL quando cancelado pelo usuario.
			reclock ("SA6", .F.)
			sa6 -> a6_salatu = _nNovoSld
			msunlock ()
			
			// O job de atualizacao do fluxo de caixa busca na tabela SE8
			se8 -> (dbsetorder (1)) // E8_FILIAL, E8_BANCO, E8_AGENCIA, E8_CONTA, E8_DTSALAT, R_E_C_N_O_, D_E_L_E_T_
			if ! se8 -> (dbseek (xfilial ("SE8") + sa6 -> a6_cod + sa6 -> a6_agencia + sa6 -> a6_numcon + dtos (dDataBase), .F.))
				reclock ("SE8", .T.)
			else
				reclock ("SE8", .F.)
			endif
			se8 -> e8_filial  = xfilial ("SE8")
			se8 -> e8_banco   = sa6 -> a6_cod
			se8 -> e8_agencia = sa6 -> a6_agencia
			se8 -> e8_conta   = sa6 -> a6_numcon
			se8 -> e8_dtsalat = dDataBase
			se8 -> e8_salatua = _nNovoSld
			se8 -> e8_moeda   = ' 1'  // Tem espaco na frente, mesmo.
			msunlock ()

			u_help ("Saldo alterado.")
		endif
	endif
return
