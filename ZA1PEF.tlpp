// Programa...: ZA1PEF
// Autor......: Robert Koch
// Data.......: 25/03/2022
// Descricao..: Verifica se pode enviar uma etiqueta para o FullWMS - GLPI 11825

// Tags para automatizar catalogo de customizacoes:
// #TipoDePrograma    #Processamento
// #Descricao         #Verifica se pode enviar para o FullWMS uma etiqueta
// #PalavasChave      #etiqueta #FullWMS
// #TabelasPrincipais #ZA1
// #Modulos           #PCP

// Historico de alteracoes
//

// ------------------------------------------------------------------------------------
user function ZA1PEF (_sEtiq)
	Local _aAreaAnt  := U_ML_SRArea ()
	local _lContinua := .T.

	u_log2 ('info', 'Verificando se a etiqueta ' + _sEtiq + ' pode ser enviada para o FullWMS.')

	if _lContinua
		za1 -> (dbsetorder (1))  // ZA1_FILIAL+ZA1_CODIGO+ZA1_DATA+ZA1_OP
		if ! za1 -> (dbseek (xfilial ("ZA1") + _sEtiq, .F.))
			u_help ("Impossivel enviar etiqueta '" + _sEtiq + "' para o FullWMS: etiqueta nao cadastrada!",, .t.)
			_lContinua = .F.
		else
			if za1 -> za1_impres != 'S'
				u_help ("Impossivel enviar etiqueta '" + _sEtiq + "' para o FullWMS: ainda nao impressa.",, .t.)
				_lContinua = .f.
			endif
		endif
	endif

	if _lContinua .and. left (_sEtiq, 1) == '0'
		u_Help ("Impossivel enviar etiqueta '" + _sEtiq + "' para o FullWMS: Etiquetas iniciadas por '0' sao geradas diretamente pelo FullWMS.",, .t.)
		_lContinua = .F.
	endif

	// Se o produto nao pode ser enviado para o FulLWMS, nem adianta enviar a etiqueta.
	if _lContinua .and. ! U_SB1PEF (za1 -> za1_prod)
		u_log2 ('erro', 'Envio da etiqueta ' + _sEtiq + ' para o FullWMS nao vai ser permitido por que o produto da etiqueta nao pode ser enviado para o FullWMS.')
		_lContinua = .F.
	endif

	// Busca validade do lote conforme origem da etiqueta.
	if _lContinua
		if U_ZA1DVld (_sEtiq) <= dDataBase
			u_log2 ('erro', 'Envio da etiqueta ' + _sEtiq + ' para o FullWMS nao vai ser permitido por a data de validade do lote deve ser maior que a data atual (verifique prazo de validade no cadastro do produto, na OP ou na NF (se for o caso).')
			_lContinua = .F.
		endif
	endif

	U_ML_SRArea (_aAreaAnt)
return _lContinua
